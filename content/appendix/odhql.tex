\chapter{ODHQL-Syntax}\label{app:odhql-syntax}
% in python console:

% import re
% grammar = """ <copy grammar from parser.py here """
% print '\n\n'.join([re.sub('\s?([a-zA-Z]+)\s', ' <\\1> ', l) for l in grammar.splitlines() if len(l) > 0])

% copy result into the grammar env below

\begin{grammar}
\small
    
    <UnionQuery> ::= <Query> ( "union" <Query> )* ( <OrderByList> )?

    <Query> ::= <FieldSelectionList>  <DataSourceSelectionList> ( <FilterList> )?

    <FieldSelectionList> ::= "select" <FieldSelection> ( "," <FieldSelection> )*

    <FieldSelection> ::= <Field> | <Expression> "as" Alias

    <CaseExpression> ::= "case" ( "when" <Condition> "then" <Expression> )+  ( "else" <Expression> )? "end"

    <Expression> ::= <Function> | <LiteralExpression> | <Field> | CaseExpression

    <Function> ::= <Identifier> "(" ( <FunctionArgumentList> )? ")"

    <FunctionArgumentList> ::= <Expression> ( ( "," <Expression> )* )?

    <Field> ::= <DataSourceNameOrAlias> "." FieldName

    <DataSourceNameOrAlias> ::= <DataSourceName> | Alias

    <DataSourceSelectionList> ::= "from" <DataSourceName> ( "as"? <Alias> )? ( <JoinDefinition> )*

    <JoinDefinition> ::= ("left" | "right" | "full" )? "join" <DataSourceName> ( "as"? <Alias> )? "on" JoinCondition

    <JoinCondition> ::= <SingleJoinCondition> | "(" <SingleJoinCondition> ( "and" <SingleJoinCondition> )* ")"

    <SingleJoinCondition> ::= <Expression> "=" Expression

    <FilterList> ::= "where" FilterAlternative

    <FilterAlternative> ::= <FilterCombination> ( "or" <FilterCombination> )*

    <FilterCombination> ::= <Condition> ( "and" <Condition> )*

    <Condition> ::= <BinaryCondition> | <InCondition> | <IsNullCondition> | <PredicateCondition> \\
                 | "(" <FilterAlternative> ")"

    <BinaryCondition> ::= <Expression>  <BinaryOperator> Expression

    <BinaryOperator> ::= "=" | "!=" | "<=" | "<"| ">=" | ">" | ( "not" )? "like"

    <InCondition> ::= <Expression> ( "not" )? "in" "(" <Expression> ( "," <Expression> )* ")"

    <IsNullCondition> ::= <Field> "is" ( "not" )? Null

    <PredicateCondition> ::= ( "not" )? Function

    <OrderByList> ::= "order" "by" <OrderByField> ( "," <OrderByField> )*

    <OrderByField> ::= ( <Field> | <Alias> | Position) ( "asc" | "desc" )?

    <Integer> ::= ( "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" )+

    <LiteralExpression> ::= <SingleQuotedString> | <Number> | <Boolean> | Null

    <Number> ::= <Integer> | Float

    <Float> ::= <Integer> "." Integer

    <Boolean> ::= "true" | "false"

    <Null> ::= "null"

    <SingleQuotedString> ::= " <string>  <in>  <single> quotes"

    <DoubleQuotedString> ::= " <string>  <in>  <double> quotes"

    <DataSourceName> ::= Identifier

    <FieldName> ::= Identifier

    <Alias> ::= Identifier

    <Identifier> ::= ( "a..z" | "A..Z" | "_" ) ( "a..z" | "A..Z" | "_" | <Integer> )* | DoubleQuotedString

\end{grammar}